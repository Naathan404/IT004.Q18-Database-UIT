-- Họ tên: Nguyễn Chí Nguyên
-- MSSV: 24521186
-- Lớp: IT004.Q18.1

----De 1
--1. Viết các câu lệnh SQL tạo các quan hệ trên với các kiểu dữ liệu mô tả trong bảng sau (tạo các ràng buộc khóa chính, khóa ngoại tương ứng): (3 đ)
CREATE DATABASE QUANLYSACH
GO
USE QUANLYSACH
GO

CREATE TABLE TACGIA(
	MATG CHAR(5) PRIMARY KEY,
	HOTEN VARCHAR(20),
	DIACHI VARCHAR(50),
	NGSINH SMALLDATETIME,
	SODT VARCHAR(15)
);

CREATE TABLE SACH(
	MASACH CHAR(5) PRIMARY KEY,
	TENSACH VARCHAR(25),
	THELOAI VARCHAR(25)
);

CREATE TABLE TACGIA_SACH(
	MATG CHAR(5) FOREIGN KEY REFERENCES TACGIA(MATG),
	MASACH CHAR(5) FOREIGN KEY REFERENCES SACH(MASACH),
	PRIMARY KEY (MATG, MASACH)
);

CREATE TABLE PHATHANH(
	MAPH CHAR(5) PRIMARY KEY,
	MASACH CHAR(5) FOREIGN KEY REFERENCES SACH(MASACH),
	NGAYPH SMALLDATETIME,
	SOLUONG INT,
	NHAXUATBAN VARCHAR(20)
);

--2. Hiện thực các ràng buộc toàn vẹn sau:
--2.1. Ngày phát hành sách phải lớn hơn ngày sinh của tác giả. (1.5 đ)
GO
CREATE TRIGGER TRG_NGPH_NGSINH
ON PHATHANH
FOR INSERT, UPDATE
AS
BEGIN
	IF EXISTS (
		SELECT *
		FROM TACGIA_SACH TGS
			INNER JOIN INSERTED I ON I.MASACH = TGS.MASACH
			INNER JOIN TACGIA TG ON TG.MATG = TGS.MATG
		WHERE TG.NGSINH >= I.NGAYPH
	)
	BEGIN
		PRINT 'NGAY PHAT HANH SAI'
		ROLLBACK TRANSACTION
	END
END
GO

--2.2. Sách thuộc thể loại “Giáo khoa” chỉ do nhà xuất bản “Giáo dục” phát hành. (1.5 đ)
CREATE TRIGGER TRG_GIAOKHOA
ON PHATHANH
FOR INSERT, UPDATE
AS
BEGIN
	IF EXISTS (
		SELECT *
		FROM INSERTED I
			JOIN SACH S ON I.MASACH = S.MASACH
		WHERE S.THELOAI = 'Giáo khoa'
			AND I.NHAXUATBAN != 'Giáo dục'
	)
	BEGIN
		PRINT 'NHA XUAT BAN KHONG HOP LE'
		ROLLBACK TRANSACTION
	END
END
GO

--3. Viết các câu lệnh SQL thực hiện các câu truy vấn sau:
--3.1. Tìm tác giả (MaTG,HoTen,SoDT) của những quyển sách thuộc thể loại “Văn học” do nhà xuất bản Trẻ phát hành. (1.5 đ)
SELECT DISTINCT TG.MATG, TG.HOTEN, TG.SODT
FROM TACGIA TG
	INNER JOIN TACGIA_SACH TGS ON TGS.MATG = TG.MATG
	INNER JOIN SACH S ON S.MASACH = TGS.MASACH
	INNER JOIN PHATHANH PH ON PH.MASACH = S.MASACH
WHERE S.THELOAI = 'Văn học'
	AND PH.NHAXUATBAN = 'nhà xuất bản Trẻ';


--3.2. Tìm nhà xuất bản phát hành nhiều thể loại sách nhất.(1.5 đ)
SELECT TOP 1 WITH TIES PH.NHAXUATBAN
FROM PHATHANH PH
	INNER JOIN SACH S ON S.MASACH = PH.MASACH
GROUP BY PH.NHAXUATBAN
ORDER BY COUNT(DISTINCT S.THELOAI) DESC;

--3.3. Trong mỗi nhà xuất bản, tìm tác giả (MaTG,HoTen) có số lần phát hành nhiều sách nhất. (1 đ)
SELECT DISTINCT PH.NHAXUATBAN, TG.MATG, TG.HOTEN
FROM PHATHANH PH
	JOIN SACH S ON S.MASACH = PH.MASACH
	JOIN TACGIA_SACH TGS ON TGS.MASACH = S.MASACH
	JOIN TACGIA TG ON TG.MATG = TGS.MATG
WHERE TG.MATG IN (
	SELECT TOP 1 WITH TIES TGS2.MATG
	FROM PHATHANH PH2
		JOIN SACH S2 ON S2.MASACH = PH2.MASACH
		JOIN TACGIA_SACH TGS2 ON TGS2.MASACH = S2.MASACH
	WHERE PH2.NHAXUATBAN = PH.NHAXUATBAN
	GROUP BY TGS2.MATG
	ORDER BY COUNT(PH2.MAPH) DESC
)
ORDER BY PH.NHAXUATBAN;
